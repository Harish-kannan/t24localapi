****************************************************************
* VERSION             : 1.0
* DATE CREATED        : 03/Jul/2019
* AUTHOR              : Chua, Jayson
* DESCRIPTION         : This will merge files created by BUH.BATCH.CREATE.WRITE.FILE routine. 
*                       This will be placed on .POST or .SELECT file of Batch routine(Using Control list)
* ASSOCIATED          : $BATCH.CREATE.WRITE.FILE
*                       PGM FILE><Same with filename>
* INPUT PARAMETERS      Y.FILE.DIR  = File Directory
*                       Y.FILENAME  = File Name
*                       Y.SUBHEADER = Subheader on first row
*                       Y.TIMESTAMP = Option to place timespamp, Set yo 'Y'  
* OUTPUT PARAMETERS     Y.ERR       = Error
****************************************************************
* VERSION             :
* DATE MODIFIED       :
* MODIFIED BY         :
* UPDATES             :
****************************************************************
SUBROUTINE BATCH.MERGE.WRITE.FILE (Y.FILE.DIR,Y.FILENAME,Y.SUBHEADER,Y.FILE.EXT,Y.TIMESTAMP,Y.ERR)
    $INSERT ../T24_BP I_COMMON
    $INSERT ../T24_BP I_EQUATE
    $INSERT ../T24_BP I_BATCH.FILES
	
	IF Y.FILENAME AND Y.FILE.EXT THEN
		GOSUB OPENFILES
		GOSUB MERGE.LOG.FILE
	END
			
	RETURN

OPENFILES:
    IF Y.FILE.DIR EQ '' THEN
        Y.FILE.DIR = "&SAVEDLISTS&"
    END

    FN.DIR = Y.FILE.DIR
    F.DIR  = ""
    CALL OPF(FN.DIR,F.DIR)
	
	Y.DATE.TIME = ''
	IF Y.TIMESTAMP THEN
		Y.DATE.TIME  = TODAY : '_' : EREPLACE(OCONV(TIME(),'MTS'),":","")
	END
    RETURN

MERGE.LOG.FILE:
* Retrive Files
	SEL.CMD      = "SELECT " : FN.DIR : " '...": Y.FILENAME : Y.FILE.EXT : "'"
	CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.REC,REC.ERR)
		
	IF SEL.LIST THEN
		* Create File	
		Y.FILENAME   = Y.FILENAME : '_' : Y.DATE.TIME : Y.FILE.EXT
		Y.FILE.PATH  = Y.FILE.DIR : '/' : Y.FILENAME
		OPENSEQ Y.FILE.PATH TO F.FILE.VAR ELSE
			CREATE F.FILE.VAR ELSE NULL
		END
		
		IF Y.SUBHEADER THEN
			Y.DATA.FINAL<-1> = Y.SUBHEADER
		END		

		* Read log files and merge to one
		FOR I = 1 TO NO.OF.REC
			CALL F.READ(FN.DIR,SEL.LIST<I>,R.DATA,F.DIR,E.DIR)
			CALL F.DELETE(FN.DIR,SEL.LIST<I>)

			Y.DATA.FINAL<-1> = R.DATA
		NEXT I

		* Write to merge to one file only
		CALL F.WRITE(FN.DIR,Y.FILENAME, Y.DATA.FINAL)
	END
	ELSE
	    Y.ERR = 'Encountered error on merging file'
	END
    RETURN
	
END
