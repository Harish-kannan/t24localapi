****************************************************************
* VERSION             : 1.0
* DATE CREATED        : SEP-06-2018
* AUTHOR              : Chua, Jayson G
* DESCRIPTION         : Local API Validation Routine attached on Version INPUT.ROUTINE
*                       Validate fields based on REGEX defined at EB.XXX.GEN.PARAM
* REPORT NAME         :
* PGM FILE            : N/A
* TSA.SERVICE         : N/A
* BATCH               : N/A
* SS FILE             : N/A
* EB.API              : <Same with filename>
* Associated          : VERSION>EB.XXX.GEN.PARAM,ADD.REGEX
****************************************************************
* VERSION             : 1.1
* DATE MODIFIED       : JAN-06-2019
* MODIFIED BY         : Chua, Jayson G
* UPDATES             : Amend logic on retrieving data on Local Field data
*                     : Added variable Y.REGEX.MATCHED to support the negate expression
*                       Y.REGEX.MATCHED EQ Y = Raised Error when REGEX expression matched
*                       Y.REGEX.MATCHED EQ N = Raised Error when REGEX expression not matched
****************************************************************
* VERSION             : 1.2
* DATE MODIFIED       : JAN-14-2019
* MODIFIED BY         : Chua, Jayson G
* UPDATES             : Added support on multivalue fields
****************************************************************
* VERSION             : 1.2
* DATE MODIFIED       : AUG-23-2019
* MODIFIED BY         : Chua, Jayson G
* UPDATES             : Rename to BUH.VR.ADD.REGEX
*                     : Add Prefix 'AR-' as requirement on Param ID
****************************************************************
    SUBROUTINE VR.ADD.REGEX
    $INSERT ../T24_BP I_COMMON
    $INSERT ../T24_BP I_EQUATE
    $INSERT ../BP     I_F.EB.BUH.GEN.PARAM

    GOSUB OPENFILES
    GOSUB READFILES

    IF R.PARAM NE '' THEN
        GOSUB READ.SS
        GOSUB PROCESS
    END

    RETURN

OPENFILES:
    FN.PARAM   = "F.EB.BUH.GEN.PARAM"
    F.PARAM    = ""

    CALL OPF(FN.PARAM,F.PARAM)
    RETURN

READFILES:
    Y.PARAM.ID = "AR-" : APPLICATION : PGM.VERSION
    CALL F.READ(FN.PARAM,Y.PARAM.ID,R.PARAM,F.PARAM,Y.ERR.PARAM)
    Y.REGEX           = R.PARAM<EB.BUH7.GEN.PARAM.1>
    Y.FIELDS          = R.PARAM<EB.BUH7.GEN.PARAM.1.1>
    Y.FIELDS.CNT      = DCOUNT(Y.FIELDS,@SM)
    Y.ERROR.ID        = R.PARAM<EB.BUH7.GEN.PARAM.2>
    Y.REGEX.MATCHED   = R.PARAM<EB.BUH7.GEN.PARAM.3>
    Y.APP.NAME        = R.PARAM<EB.BUH7.REPORT.FILENAME>
    RETURN

READ.SS:
    Y.APP.NAME   = R.PARAM<EB.BUH7.REPORT.FILENAME>
    Y.APP.SS.REC = ''
    CALL GET.STANDARD.SELECTION.DETS(Y.APP.NAME, Y.APP.SS.REC)
    RETURN

PROCESS:
    FOR I = 1 TO Y.FIELDS.CNT
        Y.FIELD      = FIELD (Y.FIELDS,@SM,I,1)
        CALL FIELD.NAMES.TO.NUMBERS(Y.FIELD, Y.APP.SS.REC, Y.FIELD.NO, Y.FM,Y.VM, Y.SM, '', ERR.MSG)
        * Y.FIELD.DATA = R.NEW(Y.FIELD.NO)
        Y.FIELD.DATA     = R.NEW(Y.FIELD.NO)<1,Y.VM>
        Y.FIELD.DATA.CNT = DCOUNT(Y.FIELD.DATA,@SM)
        
        * To accommodate checking per field at multi-value
        FOR J = 1 TO Y.FIELD.DATA.CNT    
            Y.REGEX.RESULT   = REGEXP(FIELD (Y.FIELD.DATA,@SM,J,1), Y.REGEX)
            Y.DISPLAY.ERROR  = 0        
        
            * Raised Error when Regex expression matched or not    
            IF Y.REGEX.RESULT EQ 1 AND Y.FIELD.DATA NE '' AND Y.REGEX.MATCHED EQ 'Y' THEN
                Y.DISPLAY.ERROR = 1
            END
            ELSE IF Y.REGEX.RESULT EQ 0 AND Y.FIELD.DATA NE '' AND Y.REGEX.MATCHED EQ 'N' THEN 
                Y.DISPLAY.ERROR = 1        
            END
            ELSE IF Y.REGEX.RESULT LT 0 THEN
                ETEXT = 'Invalid Expression ' : Y.REGEX.RESULT : '. Check EB.XXX.GEN.PARAM Table - ' : Y.REGEX.MATCHED
                CALL STORE.END.ERROR
            END
            
            * Display Error
            IF Y.DISPLAY.ERROR EQ 1 THEN
                AF    = Y.FIELD.NO
                AV    = J
                AS    = ""
                ETEXT = Y.ERROR.ID
                CALL STORE.END.ERROR
            END
        NEXT J
    NEXT I

    RETURN

END
